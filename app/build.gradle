plugins {
    id("dev.olog.msc.app")
    id("dev.olog.msc.dagger")
    id("kotlin-android-extensions")
    id("com.google.firebase.crashlytics")
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

android {
    namespace 'dev.olog.msc'
    defaultConfig {
        applicationId "dev.olog.msc"

        buildConfigField 'String', "LAST_FM_KEY", localProperties.last_fm_key
        buildConfigField 'String', "LAST_FM_SECRET", localProperties.last_fm_secret
        buildConfigField 'String', "AES_PASSWORD", localProperties.aes_password
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            multiDexEnabled true
        }
    }


    lintOptions {
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }
}


dependencies {
    implementation project(':app-shortcuts')
    implementation project(':analytics')
    implementation project(':core')
    implementation project(':data')
    implementation project(':image-provider')
    implementation project(':media')
    implementation project(':prefs-keys')
    implementation project(':presentation')
    implementation project(':shared-android')
    implementation project(':shared-widgets')
    implementation project(':service-music')
    implementation project(':service-floating')
    implementation project(':offline-lyrics')
    implementation project(':intents')
    implementation project(':shared')

    // google architecture components
    implementation lib_x.lifecycle_runtime
    implementation lib_x.lifecycle_java8
    implementation lib_x.lifecycle_viewmodel
    implementation lib_x.lifecycle_process

    implementation lib_x.core_ktx
    implementation lib_x.appcompat
    implementation lib_x.browser
    implementation lib_x.preference

    //implementation lib_ui.blur_kit
    implementation lib_ui.custom_tabs

    // debug
    debugImplementation lib_debug.leak_canary_debug

    debugImplementation lib_debug.stetho

    // firebase
    implementation lib_analytics.core
    implementation lib_analytics.crashlytics

    implementation lib_utils.aes_crypto
}